<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="172" height="40" backgroundAlpha="1.0" 
	verticalScrollPolicy="off" horizontalScrollPolicy="off">
	
	<mx:DataGrid id="dgInfo" x="0" y="0" width="170" visible="false" showHeaders="false" 
		itemClick="selecRegistro()" keyDown="presDGRegistro(event)">
		<mx:columns>
			<mx:DataGridColumn dataField="nombre"/>
		</mx:columns>	
	</mx:DataGrid>
	
	<mx:Metadata>
		[Event(name="seleccionar", type="seleccionar")]
		[Event(name="verificar", type="verificar")]
	</mx:Metadata>
	
	<mx:Validator id="datoV" source="{txtDato}" property="text" 
	invalid="{txtDato.setFocus()}" triggerEvent="" requiredFieldError="Dato Requerido"/>
	
	<mx:Script>
		<![CDATA[
		import Data.Globales;
		import Data.Utils;
		import mx.collections.ArrayCollection;
		import mx.controls.Alert;
		import mx.core.Application;
		import mx.effects.*;
		import mx.events.EffectEvent;
		import mx.managers.PopUpManager;
		import mx.rpc.events.ResultEvent;
		import mx.rpc.soap.WebService;
		import mx.validators.Validator;	
		
		private var _xmlInfo:XML =  new XML();
		
		public var infoObj:ArrayCollection = new ArrayCollection();

		public var codigo:String;
		public var indice:int;
		public var texto:String;
		private var du:Utils;
		private var global:Globales;
			
		public function asignarIndice():void{
			this.parent.setChildIndex(this, indice);
			txtDato.setFocus();
		}
		
		public function asignarTexto(texto:String):void{
			txtDato.text = texto;
		}
		
		public function asignarCodigo(codigo:String):void{
			this.codigo = codigo;
		}
		
		public function buscarInfo():void{
			if(txtDato.text != ""){
				dgInfo.selectedIndex = -1;
				texto = txtDato.text;					
				infoObj.filterFunction = filtrarRegistro;
				infoObj.refresh();
				if(infoObj.length > 0){
					dgInfo.validateNow();
					dgInfo.scrollToIndex(0);
					dgInfo.selectedIndex = 0;
					dgInfo.visible = true;
					dgInfo.x = txtDato.x;
					dgInfo.y = txtDato.y + txtDato.height;
					if(indice == -1)
						indice = this.parent.getChildIndex(this);
					this.parent.setChildIndex(this, this.parent.numChildren - 1);	
				}	
				else
					dgInfo.visible = false;								
			}
			else{
				infoObj.filterFunction = null;
				infoObj.refresh();
				dgInfo.visible = false;
				this.codigo = "";
			}
		}
		
		private function filtrarCaptura(item:Object):Boolean{
			var isMatch:Boolean = false
			if(texto.toLowerCase().search(item.nombre.toLowerCase()) != -1)
				isMatch = true 
			return isMatch; 
		}
	
		public function filtrarRegistro(item:Object):Boolean{
			var isMatch:Boolean = false
			if(item.nombre.toString().substr(0, texto.length).toLowerCase() == texto.toLowerCase())
				isMatch = true 
			return isMatch; 
		}
		
		public function formatearInfo():void{
			var cont:int = _xmlInfo.elements().length();
			var oItem:Object;
			var item:Array = new Array;
			
			infoObj.filterFunction = null;
			infoObj.refresh();
			infoObj.removeAll();
			infoObj.refresh();
			
			for (var i:int = 0; i < cont; i++){
				oItem = new Object();
				oItem.nombre = _xmlInfo.Table[i].NOMBRE;
				oItem.codigo = _xmlInfo.Table[i].CODIGO;
				item.push(oItem);
			}
			infoObj = new ArrayCollection(item);
		}	
		
		public function init(_xmlInfo:XML, alto:Number):void{
			this.height = alto;
			this._xmlInfo = _xmlInfo;
			indice = -1;
			dgInfo.height = alto - 25;
			dgInfo.dataProvider = null;
			formatearInfo();
			dgInfo.dataProvider = infoObj;
			txtDato.addEventListener(KeyboardEvent.KEY_DOWN, presRegistro);
		}
		
		public function limpiar():void{
			txtDato.text = "";
			codigo = "";
		}
		
		public function obtenerCodigo():String{
			return this.codigo;
		}
		
		public function obtenerDato():String{
			return txtDato.text;
		}
			
		private function presRegistro(event:KeyboardEvent):void{
			if(event.keyCode == Keyboard.DOWN){
				dgInfo.setFocus();
			}				
		}
		
		private function presDGRegistro(event:KeyboardEvent):void{
			if(event.keyCode == Keyboard.ENTER){
				selecRegistro();
				txtDato.setFocus();
			}					
		}
		
		public function selecRegistro():void{
			if(dgInfo.selectedIndex >= 0){
				var indice:int = dgInfo.selectedIndex;
				
				if (codigo != infoObj[indice].codigo){			
					txtDato.text = infoObj[indice].nombre;
					codigo = infoObj[indice].codigo;
					dispatchEvent(new Event("seleccionar"));
				}
				else{
					txtDato.text = infoObj[indice].nombre;
				}
				dgInfo.visible = false;
			}
			asignarIndice();
		}
		
		public function validarDato():Boolean{
			var invalidArray:Array;
					
			invalidArray = Validator.validateAll([datoV]);
			if(invalidArray.length == 0)
				return true;
			return false;
		}
		
		public function verificarInfo():void{
			if (dgInfo.selectedIndex < 0 || txtDato.text == ""){
				dgInfo.visible = false;
				texto = txtDato.text;
				infoObj.filterFunction = filtrarCaptura;
				infoObj.refresh();
				if (infoObj.length == 0){
					txtDato.text = "";
					codigo = "";
					dispatchEvent(new Event("verificar"));
				}
				infoObj.filterFunction = null;
				infoObj.refresh();
			}
		}
			
		]]>
	</mx:Script>
		<mx:TextInput id="txtDato" width="170" x="1" y="1" change="buscarInfo()" enter="selecRegistro()" focusOut="verificarInfo()" 
			restrict="A-Z;a-z;Ñ;ñ;0-9; " keyDown="presRegistro(event)" visible="true"/>
</mx:Canvas>